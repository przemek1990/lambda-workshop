apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "recsys.workshop"
version = "1.0"

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
    compile "com.amazonaws:aws-lambda-java-core:1.0.0",
            "com.amazonaws:aws-lambda-java-events:1.0.0",
            "com.amazonaws:aws-java-sdk-sts:1.10.19"
    testCompile "junit:junit:4.12",
            "org.mockito:mockito-core:1.10.19",
            "org.easytesting:fest-assert:1.4"
}

test {
    afterTest { desc, result ->
        println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

build.dependsOn jar

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

build.dependsOn buildZip